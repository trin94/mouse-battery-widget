# SPDX-FileCopyrightText: Elias Mueller
#
# SPDX-License-Identifier: MIT

set lazy

PLUGIN_ID := 'mouseBatteryWidget'
LINK := config_directory() / 'DankMaterialShell' / 'plugins' / file_name(justfile_directory())

alias fmt := format

# Preview the widget in a mock bar against a mocked mouse
mod mock 'src/mock'

[private]
@default:
    just --list --unsorted

# Set up the development environment
[group('dev')]
init:
    uv sync
    uv run src/qml_tooling.py

# Run all formatting and lint hooks
[group('dev')]
format:
    uv run prek run --all-files

# Update the pre-commit hook versions
[group('dev')]
update-hooks:
    uv run prek auto-update

# Upgrade all Python dependencies
[group('dev')]
update-dependencies:
    uv sync --upgrade

# Run the system tests
[group('dev')]
test *args:
    uv run src/test_mouse_battery.py {{ args }}

# Run the QML unit tests
[group('dev')]
test-qml *args:
    uv run src/qml_test_main.py {{ args }}

# List all plugins and their state
[group('dms')]
list:
    dms ipc call plugins list

# Check whether the plugin is running
[group('dms')]
status:
    dms ipc call plugins status {{ PLUGIN_ID }}

# Reload the plugin after making changes
[group('dms')]
reload:
    dms ipc call plugins reload {{ PLUGIN_ID }}

# Restart DMS with a fresh plugin symlink
[group('dms')]
restart:
    rm -f {{ LINK }}
    dms restart
    ln -s {{ justfile_directory() }} {{ LINK }}
